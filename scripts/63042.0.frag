/*
 * Original shader from: https://www.shadertoy.com/view/WsBcDh
 */

#ifdef GL_ES
precision mediump float;
#endif

// glslsandbox uniforms
uniform float time;
uniform vec2 resolution;

// shadertoy emulation
#define iTime time
#define iResolution resolution

// --------[ Original ShaderToy begins here ]---------- //
#define ROTATIONS 3.
#define WAVES 16.0
#define C 3.88651
#define FREQ 8.0
#define SPEED 0.5

vec3 gradient(in float t){        
    vec3 a = vec3(0.5,0.5,0.5);
    vec3 b = vec3 (0.5,0.5,0.5);
    vec3 c = vec3(2.0,1.0,1.0);
    vec3 d = vec3(0.2,0.20,0.25);
    return a + b*cos( 6.28318*(c*t+d) );
}



float interp(in float t) {
    t = mod(t + 2., 7.);
    float a = 0.103093;
    if (t <= 1.) {
    	return a * t * t * t * t * (1.5 + (-1.8 + 0.6 * t) * t);   
    }
    if (t <= 2.) {
    	return a * (-0.3+0.6 * t);
    }
    if (t <= 3.) {
        return a * (279.7 + t * (-716.2 + t * (756. + t*(-420.+t*(129.5+t*(-21.+1.4*t))))));
    }
    if (t <= 4.) {
    	return a * (-3.8 + 2.* t);   
    }
    if (t <= 5.) {
        return a * (7804.2 + t * (-10622.+ t * (6000.+t*(-1800.+t*(302.5+(-27.+t)*t)))));
    }
    if (t <= 7.) {
        float tt = t - 6.;
        
        return 0.95167525773194 + 
            tt * (0.1546391752577319 + tt * (-0.14497422680412372 + 
    0.04832474226804124  * tt * tt - 0.009664948453608248 * tt * tt * tt * tt));
            
   }
    return 1.;
}


vec2 pos(in float t) {
	float theta = 6.2831853 * ROTATIONS * interp(t);
    
    return vec2(
    	sin(theta),
        cos(theta)
    );
    
    
}

float wave(in float t, in vec2 uv) {
    float result = 0.;
    for (float i = 0.; i < WAVES; ++i) {
        float td = t - i / FREQ;
        td -= mod(td, 1. / FREQ);
        vec2 ptd = pos(td);
        float r =  (t - td) * C;
        float d = distance(uv, ptd) - r;
        result += exp(- 180. * d * d / (0.2 + r)) / (5. + 5. * r);
        
        
    }
    return 2. * result + 0.2;
    
}


void mainImage(out vec4 fragColor, in vec2 fragCoord) {
    vec2 uv = 2. * (fragCoord - 0.5 * iResolution.xy) / iResolution.y;
    
    uv *= 2.5;
    
    float t = iTime * SPEED;
 	
    vec3 col = gradient(wave(t, uv));
    
    col += 1. - smoothstep(
        0.145, 0.16,
        abs(distance(uv, pos(t)))    
    );
    
    col += 1. - smoothstep(
        0.0, 0.05,
        abs(dot(uv, uv) - 1.)    
    );
    
    
    fragColor = vec4(col, 1.);
}
// --------[ Original ShaderToy ends here ]---------- //

void main(void)
{
    mainImage(gl_FragColor, gl_FragCoord.xy);
}