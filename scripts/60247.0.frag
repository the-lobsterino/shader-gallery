//BREXIT
#ifdef GL_ES
precision highp float;
#endif

// glslsandbox uniforms
uniform float time;
uniform vec2 resolution;

// shadertoy emulation
#define iTime time
#define iResolution resolution

// --------[ Original ShaderToy begins here ]---------- //
#define MAX_DIST 100.0
#define MAX_STEPS 128
#define SURF_DIST 0.0001
#define M_PI 3.14159
#define AA 1
//#define AA 2

#define MATERIAL_BODY 1
#define MATERIAL_BELLY 2
#define MATERIAL_SKIN_YELLOW 3
#define MATERIAL_EYE 4
#define MATERIAL_FLOOR 5
#define MATERIAL_COCK 6

struct Hit {
    float d;
    int material;
};

#define CHS 0.18
float sdBox2(in vec2 p,in vec2 b) {vec2 d=abs(p)-b;return length(max(d,vec2(0))) + min(max(d.x,d.y),0.0);}
float line2(float d,vec2 p,vec4 l){vec2 pa=p-l.xy;vec2 ba=l.zw-l.xy;float h=clamp(dot(pa,ba)/dot(ba,ba),0.0,1.0);return min(d,length(pa-ba*h));}
float TB(vec2 p, float d){p.y=abs(p.y);return line2(d,p,vec4(2,3.25,-2,3.25)*CHS);}
float B(vec2 p,float d){p.y+=1.75*CHS;d=min(d,abs(sdBox2(p,vec2(2.0,1.5)*CHS)));p+=vec2(0.5,-3.25)*CHS;return min(d,abs(sdBox2(p,vec2(1.5,1.75)*CHS)));} float E(vec2 p,float d){d=TB(p,d);d=line2(d,p,vec4(-2,3.25,-2,-3.25)*CHS);return line2(d,p,vec4(0,-0.25,-2,-0.25)*CHS);} float I(vec2 p,float d){d=line2(d,p,vec4(0,-3.25,0,3.25)*CHS);p.y=abs(p.y);return line2(d,p,vec4(1.5,3.25,-1.5,3.25)*CHS);} float R(vec2 p,float d){d=line2(d,p,vec4(0.5,-0.25,2,-3.25)*CHS);d=line2(d,p,vec4(-2,-3.25,-2,0.0)*CHS);p.y-=1.5*CHS;return min(d, abs(sdBox2(p,vec2(2.0,1.75)*CHS)));} float T(vec2 p,float d){d=line2(d,p,vec4(0,-3.25,0,3.25)*CHS);return line2(d,p,vec4(2,3.25,-2,3.25)*CHS);} float X(vec2 p,float d){d = line2(d,p,vec4(-2,3.25,2,-3.25)*CHS);return line2(d,p,vec4(-2,-3.25,2,3.25)*CHS);} // DOGSHIT

float GetText(vec2 uv)
{
	uv.y -= 2.4+sin(uv.x+time)*0.2;
	uv.x += 2.75;
	float d = B(uv,1.0);uv.x -= 1.1;
	d = R(uv,d);uv.x -= 1.1;
	d = E(uv,d);uv.x -= 1.1;
	d = X(uv,d);uv.x -= 1.1;
	d = I(uv,d);uv.x -= 1.1;
	d = T(uv,d);
	d = smoothstep(0.0,0.05,d-0.55*CHS);
	return d;
}
	
	
// Distance to sphere at origin of radius `r`
float sd_sphere(vec3 p, float r) {
    return length(p) - r;
}

// Distance to capsule of radius `r` formed by line segment from `a` to `b`
float sd_capsule(vec3 p, vec3 a, vec3 b, float r) {
    vec3 ap = p - a;
    vec3 ab = b - a;
    float t = clamp(dot(ap, ab) / dot(ab, ab), 0.0, 1.0);
    return length(ap - ab*t) - r;
}

// Distance to plane at origin of normal `n`
float sd_plane(vec3 p, vec3 n) {
    return dot(p, n);
}

// Distance to torus at origin of radius `r`
float sd_torus(vec3 p, vec2 r) {
    float x = length(p.xz) - r.x;
    return length(vec2(x, p.y)) - r.y;
}

// Distance to ellipsoid at origin of radius `r`
float sd_ellipsoid(vec3 p, vec3 r) {
    float k0 = length(p/r);
    float k1 = length(p/(r*r));
    return k0*(k0-1.0)/k1;
}

// Distance to round cone
float sd_round_cone(vec3 p, float r1, float r2, float h) {
    vec2 q = vec2(length(p.xz), p.y);
      
    float b = (r1-r2)/h;
    float a = sqrt(1.0-b*b);
    float k = dot(q,vec2(-b,a));
      
    if( k < 0.0 ) return length(q) - r1;
    if( k > a*h ) return length(q-vec2(0.0,h)) - r2;
          
    return dot(q, vec2(a,b) ) - r1;
}

// Unite two SDFs smoothing
float op_smooth_union(float d1, float d2, float k ) {
    float h = clamp(0.5 + 0.5*(d2-d1)/k, 0.0, 1.0);
    return mix( d2, d1, h ) - k*h*(1.0-h);
}

// Rotate vector `p` along the x axis at angle `t` (in radians)
vec3 rotate_x(vec3 p, float t) {
    float cost = cos(t);
    float sint = sin(t);
    mat3 a = mat3(
        1.0,  0.0,  0.0,
        0.0, cost,-sint,
        0.0, sint, cost);
    return a * p;
}

// Rotate vector `p` along the z axis by angle `t` (in radians)
vec3 rotate_z(vec3 p, float t) {
    float cost = cos(t);
    float sint = sin(t);
    mat3 a = mat3(
         cost,-sint, 0.0,
         sint, cost, 0.0,
          0.0,  0.0, 1.0);
    return a * p;
}

#define check_hit(m) if(dist < mindist) { material = m; mindist = dist; }

void get_penguin_sdf(vec3 p, inout float mindist, inout int material) {
    // body
    float dist = op_smooth_union(
        sd_ellipsoid(p - vec3(0.0, 1.0, 0.0), vec3(2.0, 2.2, 1.8)),
        sd_sphere(p - vec3(0.0, 3.0, 0.0), 1.6), 0.5);

    // arms
    const vec3 larm_pos = vec3(-2.0, 2.0, 0);
    const vec3 rarm_pos = larm_pos*vec3(-1.0, 1.0, 1.0);
    const vec3 arm_radius = vec3(0.9, 0.3, 0.8);
    dist = op_smooth_union(dist,
        sd_ellipsoid(rotate_z(p - larm_pos, 0.7 + sin(iTime*10.0)*0.2), arm_radius), 0.1);
    dist = op_smooth_union(dist,
        sd_ellipsoid(rotate_z(p - rarm_pos,-0.7 + sin(iTime*10.0)*0.2), arm_radius), 0.1);

    // rings around eyes
    const vec3 leye_pos = vec3(-0.5, 3.2, 1.0);
    const vec3 reye_pos = leye_pos*vec3(-1.0, 1.0, 1.0);
    const vec3 leye_size = vec3(0.8, 0.9, 0.7);
    const vec3 reye_size = vec3(0.8, 0.7, 0.7);
    dist = op_smooth_union(dist,
        sd_torus(rotate_x(p - leye_pos, M_PI/2.0), vec2(leye_size.x, 0.2)), 0.1);
    dist = op_smooth_union(dist,
        sd_torus(rotate_x(p - reye_pos, M_PI/2.0), vec2(reye_size.x, 0.2)), 0.1);

    check_hit(MATERIAL_BODY);

    // belly
    dist = sd_ellipsoid(p - vec3(0.0, 1.0, 0.4), vec3(2.0, 2.2, 1.8)*0.9);
    float zz = sin(time*3.31)*0.21;
    dist = op_smooth_union(dist,sd_round_cone(rotate_x(p - vec3(0.0, -.5, 1.3), (zz+4.7)*M_PI/2.0), 0.2, 0.35, 2.2),0.3); // COCK
    dist = op_smooth_union(dist,sd_sphere(p - vec3(0.0, -0.6, 1.3),0.85),0.5); // NUT SACK
    check_hit(MATERIAL_COCK);
    
    // foot
    const vec3 lfoot_pos = vec3(-1.2, -0.5, 1.9);
    const vec3 rfoot_pos = vec3(-lfoot_pos.x, lfoot_pos.y, lfoot_pos.z);
    const vec3 foot_radius = vec3(1.0, 0.6, 1.0);
    dist = min(
        sd_ellipsoid(rotate_x(p - lfoot_pos, sin(iTime*10.0)*0.3), foot_radius),
        sd_ellipsoid(rotate_x(p - rfoot_pos, sin(iTime*10.0+M_PI)*0.3), foot_radius));

    // beak
    dist = min(dist, sd_round_cone(rotate_x(p - vec3(0.0, 2.5, 2.0), 3.3*M_PI/2.0), 0.1, 0.4, 0.6));
    check_hit(MATERIAL_SKIN_YELLOW);

	
	
    // eyes
    dist = min(
        sd_ellipsoid(p - leye_pos, leye_size), 
        sd_ellipsoid(p - reye_pos, reye_size));
    check_hit(MATERIAL_EYE);

    // retina
    vec3 lretina_pos = vec3(-0.4 + sin(iTime)*0.1, 3.3, 1.63);
    vec3 rretina_pos = vec3(0.4 - sin(iTime)*0.1, 3.3, 1.63);
    dist = min(sd_ellipsoid(p - lretina_pos, vec3(0.2, 0.3, 0.1)),
               sd_ellipsoid(p - rretina_pos, vec3(0.2, 0.25, 0.1)));
    check_hit(MATERIAL_BODY);
}

// Return the closest surface distance to point p
Hit get_sdf(vec3 p) {
    float mindist = MAX_DIST;
    int material = 0;

    // penguin
    get_penguin_sdf(rotate_z(p, sin(iTime*10.0)*0.1), mindist, material);

    // floor
    float dist = sd_plane(p - vec3(0.0, -1.0, 0.0), normalize(vec3(0.0, 1.0, 0.0)));
    check_hit(MATERIAL_FLOOR);

    return Hit(mindist, material);
}

// Get normal at point `p` using the tetrahedron technique for computing the gradient
vec3 get_normal(vec3 p) {
    const float eps = 0.0001;
    vec2 e = vec2(1.0,-1.0);
    return normalize(e.xyy*get_sdf(p + e.xyy*eps).d + 
                     e.yyx*get_sdf(p + e.yyx*eps).d + 
                     e.yxy*get_sdf(p + e.yxy*eps).d + 
                     e.xxx*get_sdf(p + e.xxx*eps).d);
}

// March a ray from `rayfrom` along the `raydir` direction and return the closet surface distance
Hit ray_march(vec3 rayfrom, vec3 raydir) {
    // begin at ray origin
    float t = 0.0;
    Hit hit;
    // ray march loop
    for(int i=0; i<MAX_STEPS; ++i) {
        // compute next march point
        vec3 p = rayfrom+t*raydir;
        // get the distance to the closest surface
        hit = get_sdf(p);
        // increase the distance to the closest surface
        t += hit.d;
        // hit a surface
        if(hit.d < SURF_DIST || t > MAX_DIST)
            break;
    }
    // return the distance to `rayfrom`
    hit.d = t;
    return hit;
}

// Hard shadows
float hard_shadow(vec3 rayfrom, vec3 raydir, float tmin, float tmax) {
    float t = tmin;
    for(int i=0; i<MAX_STEPS; i++) {
        vec3 p = rayfrom + raydir*t;
        float h = get_sdf(p).d;
        if(h < SURF_DIST)
            return 0.0;
        t += h;
        if(t > tmax)
            break;
    }
    return 1.0;
}

// Get occlusion along `normal` from point of view `rayfrom`
float get_occlusion(vec3 rayfrom, vec3 normal) {
    const int AO_ITERATIONS = 5;
    const float AO_START = 0.01;
    const float AO_DELTA = 0.11;
    const float AO_DECAY = 0.95;
    const float AO_INTENSITY = 2.0;

    float occ = 0.0;
    float decay = 1.0;
    for(int i=0; i<AO_ITERATIONS; ++i) {
        float h = AO_START + float(i) * AO_DELTA;
        float d = get_sdf(rayfrom + h*normal).d;
        occ += (h-d) * decay;
        decay *= AO_DECAY;
    }
    return clamp(1.0 - occ * AO_INTENSITY, 0.0, 1.0);
}

// Return diffuse albedo color for material
vec3 get_material_diffuse(vec3 p, int material) {
    if (material == MATERIAL_BODY) {
        return vec3(0.0, 0.0, 0.0);
    }
    else if (material == MATERIAL_BELLY) {
        return vec3(0.6, 0.6, 0.6);
    }
    else if (material == MATERIAL_FLOOR) {
        float checker = clamp(sin(p.x)*sin(p.y)*sin(p.z+iTime*4.0), 0.0, 1.0);
        return vec3(0.4, 0.4, 0.4) * checker + vec3(0.1, 0.2, 0.2);
    }
    else if (material == MATERIAL_SKIN_YELLOW) {
        return vec3(1.0, .3, .01);
    }
    else if (material == MATERIAL_EYE) {
        return vec3(1.0, 1.0, 1.0);
    }
    else if (material == MATERIAL_COCK) {
        return vec3(1.0, 0.4, 0.3);
    }
    else {
            return vec3(1.0, 1.0, 1.0);
    }
}

// Return specular color for material
vec3 get_material_specular(vec3 p, int material) {
    if (material == MATERIAL_BODY) {
        return vec3(0.6, 0.6, 0.6);
    }
    else if (material == MATERIAL_SKIN_YELLOW) {
        return vec3(1.0, .6, .1);
    }
    else if (material == MATERIAL_EYE) {
        return vec3(1.0, 10.0, 1.0);
    }
    else if (material == MATERIAL_COCK) {
        return vec3(1.0, 3.0, .5);
    }
	
    else {
        return vec3(0.0, 0.0, 0.0);
    }
}

// Compute the scene light at a point
vec3 get_light(vec3 raydir, vec3 p, int material) {
    vec3 diffuse = vec3(0);
    vec3 specular = vec3(0);
    vec3 normal = get_normal(p);
    float occlusion = get_occlusion(p, normal);

    // sun light
    const float SUN_INTENSITY = 1.5;
    const float SUN_SHINESS = 10.0;
    const vec3 SUN_POS = vec3(-10.0, 20.0, 10.0);
    const vec3 SUN_COLOR = vec3(1.0,0.77,0.6);

    vec3 sun_vec = SUN_POS - p;
    vec3 sun_dir = normalize(sun_vec);
    float sun_diffuse = clamp(dot(normal, sun_dir), 0.0, 1.0);
    float sun_shadow = hard_shadow(p, sun_dir, 0.01, length(sun_vec));
    float sun_specular = pow(clamp(dot(reflect(sun_dir, normal), raydir), 0.0, 1.0), SUN_SHINESS);

    diffuse += SUN_COLOR * (sun_diffuse * sun_shadow * SUN_INTENSITY);
    specular += SUN_COLOR * sun_specular;

    // sky light
    const float SKY_INTENSITY = 0.3;
    const float SKY_SHINESS = 10.0;
    const float SKY_MINIMUM_ATTENUATION = 0.5;
    const vec3 SKY_COLOR = vec3(0.5, 0.7, 1.0);

    float sky_diffuse = SKY_MINIMUM_ATTENUATION + SKY_MINIMUM_ATTENUATION * normal.y;
    float sky_specular = pow(clamp(dot(reflect(vec3(0.0,1.0,0.0), normal), raydir), 0.0, 1.0), SKY_SHINESS);
    diffuse += SKY_COLOR * (SKY_INTENSITY * sky_diffuse * occlusion);
    specular += SKY_COLOR * (sky_specular * occlusion);

    // fake indirect light
    const float INDIRECT_INTENSITY = 0.2;
    const float INDIRECT_SHINESS = 10.0;
    const vec3 INDIRECT_COLOR = SUN_COLOR;

    vec3 ind_dir = normalize(sun_dir * vec3(-1.0,0.0,-1.0));
    float ind_diffuse = clamp(dot(normal, ind_dir), 0.0, 1.0);
    float ind_specular = pow(clamp(dot(reflect(ind_dir, normal), raydir), 0.0, 1.0), INDIRECT_SHINESS);
    diffuse += INDIRECT_COLOR * (ind_diffuse * INDIRECT_INTENSITY);
    specular += INDIRECT_COLOR * (ind_specular * INDIRECT_INTENSITY);

    // env light
    const vec3 ENV_COLOR = SKY_COLOR;
    const float ENV_INTENSITY = 0.3;
    diffuse += ENV_COLOR * ENV_INTENSITY;

    // apply material
    vec3 col = diffuse * get_material_diffuse(p, material) +
               specular * get_material_specular(p, material);

    // gamma correction
    col = pow(col, vec3(0.4545));

    return col;
}

// Return camera transform matrix looking from `lookfrom` towards `lookat`, with tilt rotation `tilt`,
// vertical field of view `vfov` (in degrees), at coords `uv` (in the range [-1,1])
vec3 get_ray(vec3 lookfrom, vec3 lookat, float tilt, float vfov, vec2 uv) {
    // camera up vector
    vec3 vup = vec3(sin(tilt), cos(tilt), 0.0);
    // camera look direction
    vec3 lookdir = normalize(lookat - lookfrom);
    // unit vector in camera x axis
    vec3 u = cross(lookdir, vup);
    // unit vector in camera y axis
    vec3 v = cross(u, lookdir);
    // vector in camera z axis normalized by the fov
    vec3 w = lookdir * (1.0 / tan(vfov*M_PI/360.0));
    // camera transformation matrix
    mat3 t = mat3(u, v, w);
    // camera direction
    return normalize(t * vec3(uv, 1.0));
}

vec3 render(vec2 uv) {
    vec3 lookfrom = vec3(0, 10, 25);
    vec3 lookat = vec3(0, 1.8, 0);
    vec3 raydir = get_ray(lookfrom, lookat, 0.0, 20.0, uv);
    Hit hit = ray_march(lookfrom, raydir);
    vec3 p = lookfrom + raydir * hit.d;
    vec3 col = get_light(raydir, p, hit.material);
    return col;
}

vec3 render_aa(vec2 uv) {
#if AA > 1
    float w = 1.0/iResolution.y;
    vec3 col = vec3(0.0);
    for(int n=0; n<AA*AA; ++n) {
        vec2 o = 2.0*(vec2(float(int(n / AA)),mod(float(n),float(AA))) / float(AA) - 0.5);
        col += render(uv + o*w);
    }
    col /= float(AA*AA);
    return col;
#else
    return render(uv);
#endif
}

void mainImage(out vec4 fragcolor, in vec2 fragcoord) {
    // uv coords in range from [-1,1] for y and [-aspect_ratio,aspect_ratio] for x
    vec2 uv = 2.0 * ((fragcoord-0.5*iResolution.xy) / iResolution.y);
    // render the entire scene
    vec3 col = render_aa(uv);
    // set the finished color
	
	//float dd= GetText(uv*3.5);
	//col = mix(col*0.1+vec3(.36,0.5,.04)+(abs(uv.x)*0.46+uv.y*0.3), col,dd);
	
	
    fragcolor = vec4(col,1);
}
// --------[ Original ShaderToy ends here ]---------- //

void main(void)
{
    mainImage(gl_FragColor, gl_FragCoord.xy);
}