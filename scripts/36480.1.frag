#ifdef GL_ES
precision mediump float;
#endif

#extension GL_OES_standard_derivatives : enable

uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

// Created by Stephane Cuillerdier - Aiekick/2016
// License Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.

/* 
	Abstract Plane Cloudy Beauty

	original from shadertoy : https://www.shadertoy.com/view/4ttXzr#

	this shader is a mix of :

 	Shader Cloudy spikeball from duke : https://www.shadertoy.com/view/MljXDw
	Shader Cloudy MegaWave from me : https://www.shadertoy.com/view/MljSRy
	Shader Abstract Plane Beauty from me : https://www.shadertoy.com/view/MlcSzn
*/

const vec3 lightDir = vec3(0.,1., 0.5);
const float mPi = 3.14159;
const float m2Pi = 6.28318;

float t = 0.;

float pn(vec3 p) {
   vec3 i = floor(p);
   vec4 a = dot(i, vec3(1., 57., 21.)) + vec4(0., 57., 21., 78.);
   vec3 f = cos((p-i)*mPi)*(-.5) + .5;
   a = mix(sin(cos(a)*a), sin(cos(1.+a)*(1.+a)), f.x);
   a.xy = mix(a.xz, a.yw, f.y);
   return mix(a.x, a.y, f.z);
}

float fpn(vec3 p) 
{
	return pn(p*.06125)*.5 + pn(p*.125)*.25 + pn(p*.25)*.125;
}
/////////////////////////

mat3 RotZ(float a){return mat3(cos(a),-sin(a),0.,sin(a),cos(a),0.,0.,0.,1.);}

vec3 path(vec3 p)
{
    p.xy += cos(time * 0.2);
	p *= RotZ(p.z * 0.1);
    p += sin(p.zxy * 0.5) * 0.5;
	p *= RotZ(p.z * 0.2);
    p = sin(p.zxy * 0.2) * 2.;
    return p;
}

float disp(vec3 p)
{
    p *= 50.;
   	return fpn(p) * .6;
}


float dfBase(vec3 p)
{
	p += path(p);
	p *= RotZ(p.z * 0.045);
    return 1. - cos(p.z*1.5) * 0.1 - abs(p.y);
}

float df(vec3 p)
{
	return dfBase(p) + disp(p);
}

vec3 nor( vec3 pos, float prec )
{
	vec3 eps = vec3( prec, 0., 0. );
	vec3 nor = vec3(
	    dfBase(pos+eps.xyy) - dfBase(pos-eps.xyy),
	    dfBase(pos+eps.yxy) - dfBase(pos-eps.yxy),
	    dfBase(pos+eps.yyx) - dfBase(pos-eps.yyx) );
	return normalize(nor);
}

// return color from temperature 
//http://www.physics.sfasu.edu/astro/color/blackbody.html
//http://www.vendian.org/mncharity/dir3/blackbody/
//http://www.vendian.org/mncharity/dir3/blackbody/UnstableURLs/bbr_color.html
vec3 blackbody(float Temp)
{
	vec3 col = vec3(255.);
    col.x = 56100000. * pow(Temp,(-3. / 2.)) + 148.;
   	col.y = 100.04 * log(Temp) - 623.6;
   	if (Temp > 6500.) col.y = 35200000. * pow(Temp,(-3. / 2.)) + 184.;
   	col.z = 194.18 * log(Temp) - 1448.6;
   	col = clamp(col, 0., 255.)/255.;
    if (Temp < 1000.) col *= Temp/1000.;
   	return col;
}

// get density of the df at surfPoint
// ratio between constant step and df value
float SubDensity(vec3 surfPoint, float prec, float ms) 
{
	vec3 n;
	float s = 0.;
    const int iter = 10;
	for (int i=0;i<iter;i++)
	{
		n = nor(surfPoint,prec); 
		surfPoint = surfPoint - n * ms; 
		s += dfBase(surfPoint);
	}
	
	return 1.-s/(ms*float(iter)); // s < 0. => inside df
}

float SubDensity(vec3 p, float s) 
{
	vec3 n = nor(p,s); 							// precise normale at surf point
	return dfBase(p - n * s);						// ratio between df step and constant step
}

void main()
{
	vec2 g = gl_FragCoord.xy;
	vec2 si = resolution;
	vec2 uv = (g+g-si)/si.y;
	vec3 ro = vec3(0,0, time * 6.); ro -= path(ro);
    vec3 cv = ro + vec3(0,0,4); cv -= path(cv);
	vec3 cu = normalize(vec3(0,1,0));
  	vec3 z = normalize(cv-ro);
    vec3 x = normalize(cross(cu,z));
  	vec3 y = cross(z,x);
    float fov = .9;
  	vec3 rd = normalize(fov * (uv.x * x + uv.y * y) + z);
	
	float s = 1.;
	float d = 0.;
	vec3 p = ro + rd * d;
	
    float dMax = 50.;
	float sMin = 0.0001;
	
    /////////////////////////
	// FROM Shader Cloudy spikeball from duke : https://www.shadertoy.com/view/MljXDw
	float ld, td= 0.; // ld, td: local, total density 
	float w; // w: weighting factor
	vec3 tc = vec3(.25); // total color
   
	float h=.05;
    const float stepf = 1./250.;
	/////////////////////////
    
	for (float i=0.; (i<1.); i+=stepf) 
	{
        // FROM Shader Cloudy spikeball from duke : https://www.shadertoy.com/view/MljXDw
		if(!((i<1.) && (s>sMin) && (d < dMax)&& (td < .95))) break;
		
        s = df(p);
		s *= (s>0.001?0.15:.2) ;
        
        /////////////////////////
		// FROM Shader Cloudy spikeball from duke : https://www.shadertoy.com/view/MljXDw
		ld = (h - s) * step(s, h);
		w = (1. - td) * ld;   
		tc += w; 
      	td += w + .005;
      	s = max(s, 0.02);
        /////////////////////////
      	
        d += s;
	  	p = ro + rd * d;
    }
	
    // classic RM from Abstract Plane Beauty : https://www.shadertoy.com/view/MlcSzn
    s = 1., d = 0.;
	for (int i=0; i<30; i++) // 30 iterations yeah :)
	{
		if (log(d/1e6)>0.) break; // due to this special break condition
		d += dfBase(ro+rd*d);
	}
	
	p = ro + rd * d;											// surface point
	vec3 lid = normalize(ro-p); 										// light dir
	vec3 n = nor(p, 0.001);											// normal at surface point
	vec3 refl = reflect(rd,n);										// reflected ray dir at surf point 
	float diff = clamp( dot( n, lid ), 0.0, 1.0 ); 					// diffuse
	float fre = pow( clamp( 1. + dot(n,rd),0.0,1.0), 4. ); 			// fresnel
	float spe = pow(clamp( dot( refl, lid ), 0.0, 1.0 ),16.);		// specular
	vec3 col = vec3(.8,.5,.2);
    
    // here the magix happen
	float sss = dfBase(p - n*0.001)/0.01;								// quick sss 0.001 of subsurface
	
	float sb = SubDensity(p, 0.01, 0.1);							// deep subdensity from 0.01 to 0.1 (10 iterations)
	vec3 bb = blackbody(200. * sb);									// blackbody color
	float sss2 = 0.8 - SubDensity(p, 3.); 							// one step sub density of df of 3 of subsurface
	
	vec3 a = (diff + fre + bb * sss2 * .8 + col * sss * .2) * 0.25 + spe; // near
    vec3 b = col * sss * .5;
    
    // mix
    gl_FragColor.rgb = mix( tc, mix(b,a,1.-exp(-0.015*d*d)), 1.-exp(-0.01*d*d) );

	// vigneting from iq Shader Mike : https://www.shadertoy.com/view/MsXGWr
    vec2 q = g/si;
    gl_FragColor.rgb *= 0.5 + 0.5*pow( 16.0*q.x*q.y*(1.0-q.x)*(1.0-q.y), 0.55 );
	
	gl_FragColor.a = 1.;
}
