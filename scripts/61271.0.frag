/*
 * Original shader from: https://www.shadertoy.com/view/tl3XWB
 */

#ifdef GL_ES
precision mediump float;
#endif

// glslsandbox uniforms
uniform float time;
uniform vec2 resolution;

// shadertoy emulation
#define iTime time
#define iResolution resolution

// --------[ Original ShaderToy begins here ]---------- //
#define NUM_OCTAVES 10

float random (in vec2 _st) {
    return fract(sin(dot(_st.xy,
                         vec2(12.9898,78.233)))*
        43758.5453123);
}

// Based on Morgan McGuire @morgan3d
// https://www.shadertoy.com/view/4dS3Wd
float noise (in vec2 _st) {
    vec2 i = floor(_st);
    vec2 f = fract(_st);

    // Four corners in 2D of a tile
    float a = random(i);
    float b = random(i + vec2(1.0, 0.0));
    float c = random(i + vec2(0.0, 1.0));
    float d = random(i + vec2(1.0, 1.0));

    vec2 u = f * f * (3.0 - 2.0 * f);

    return mix(a, b, u.x) +
            (c - a)* u.y * (1.0 - u.x) +
            (d - b) * u.x * u.y;
}

float mod289(float x){return x - floor(x * (1.0 / 289.0)) * 289.0;}
vec4 mod289(vec4 x){return x - floor(x * (1.0 / 289.0)) * 289.0;}
vec4 perm(vec4 x){return mod289(((x * 34.0) + 1.0) * x);}

float noise(vec3 p){
    vec3 a = floor(p);
    vec3 d = p - a;
    d = d * d * (3.0 - 2.0 * d);

    vec4 b = a.xxyy + vec4(0.0, 1.0, 0.0, 1.0);
    vec4 k1 = perm(b.xyxy);
    vec4 k2 = perm(k1.xyxy + b.zzww);

    vec4 c = k2 + a.zzzz;
    vec4 k3 = perm(c);
    vec4 k4 = perm(c + 1.0);

    vec4 o1 = fract(k3 * (1.0 / 41.0));
    vec4 o2 = fract(k4 * (1.0 / 41.0));

    vec4 o3 = o2 * d.z + o1 * (1.0 - d.z);
    vec2 o4 = o3.yw * d.x + o3.xz * (1.0 - d.x);

    return o4.y * d.y + o4.x * (1.0 - d.y);
}


float fbm ( in vec2 _st) {
    float t = 1.;
    float v = 0.;
    float a = 0.5;
    vec2 shift = vec2(120.0);
    // Rotate to reduce axial bias
    mat2 rot = mat2(cos(0.5), sin(0.5),
                    -sin(0.5), cos(0.5));
    for (int i = 0; i < NUM_OCTAVES; ++i) {
        v += a * noise(vec3(_st, t));
        _st = rot * _st * 2.0 + shift;
        a *= 0.5;
    }
    return v;
}

vec2 addfbm(vec2 uv) {
    return uv + vec2(fbm(uv), fbm(uv+vec2(314.433623, 234.62324)));
}

vec3 makeFbm(vec2 uv) {
    vec2 noiseUv = addfbm(uv);
    noiseUv.x += iTime*.1;
    noiseUv = addfbm(noiseUv);
    noiseUv = addfbm(noiseUv);
    noiseUv = addfbm(noiseUv);
    noiseUv.x += iTime*.1;
    noiseUv = addfbm(noiseUv);

    float m = fbm(noiseUv*5.);
    vec3 col = vec3(pow(m, 4.));
    return col;

}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    vec2 uv = (fragCoord-.5*iResolution.xy)/iResolution.y;

    vec3 col = vec3(0.);
    
    vec3 ro = vec3(0,0,0);
    float z = 1.;
    vec3 rd = normalize(vec3(uv, z)-ro);
    
    float t=0.;
    float step;
    
    for(int i=0; i<128; i++){
        vec3 p = ro + rd*t;
        
        float r = makeFbm(p.xy).x*2.;
        float dS = length(p-vec3(0,0,3))-1.+r;
        dS = max(abs(dS), 0.02);

        step = float(i);
        if (t > 100. || dS < 0.01) break;
        t += dS;
    }
    
    col = vec3(pow(step/128., 1. + sin(iTime)*.5+.5));
    
    
    // Output to screen
    fragColor = vec4(col,1.0);
}
// --------[ Original ShaderToy ends here ]---------- //

void main(void)
{
    mainImage(gl_FragColor, gl_FragCoord.xy);
}