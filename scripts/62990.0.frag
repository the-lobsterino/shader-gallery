#ifdef GL_ES
precision mediump float;
#endif
uniform float time;
uniform vec2 resolution;
#define iTime time
#define iResolution resolution
#define A5
#define DLEVEL 5

const vec4 iMouse = vec4(0.0);
float SmoothMin (float a, float b, float r);
float SmoothMax (float a, float b, float r);
float Hashfv2 (vec2 p);
vec2 Hashv2v2 (vec2 p);
float Noiseff (float p);
float Noisefv2 (vec2 p);
vec3 Noisev3v2 (vec2 p);
float Fbm2 (vec2 p);
float Fbm3 (vec3 p);
vec3 VaryNf (vec3 p, vec3 n, float f);

mat3 flMat;
vec3 flPos, qHit, qnHit, fBallPos, sunDir;
vec2 gVec[7], hVec[7];
float tCur, dstFar, fBallRad;
const float pi = 3.14159;

#define SQRT3 1.73205

vec2 PixToHex (vec2 p)
{
  vec3 c, r, dr;
  c.xz = vec2 ((1./SQRT3) * p.x - (1./3.) * p.y, (2./3.) * p.y);
  c.y = - c.x - c.z;
  r = floor (c + 0.5);
  dr = abs (r - c);
  r -= step (dr.yzx, dr) * step (dr.zxy, dr) * dot (r, vec3 (1.));
  return r.xz;
}

vec2 HexToPix (vec2 h)
{
  return vec2 (SQRT3 * (h.x + 0.5 * h.y), (3./2.) * h.y);
}

void HexVorInit ()
{
  vec3 e = vec3 (1., 0., -1.);
  gVec[0] = e.yy;
  gVec[1] = e.xy;
  gVec[2] = e.yx;
  gVec[3] = e.xz;
  gVec[4] = e.zy;
  gVec[5] = e.yz;
  gVec[6] = e.zx;
  for (int k = 0; k < 7; k ++) hVec[k] = HexToPix (gVec[k]);
}

#define EPS 0.1  // controls smoothing

vec4 HexVor (vec2 p)
{
  vec4 sd, udm;
  vec2 ip, fp, d, u;
  float amp, a;
  amp = 0.7;
  ip = PixToHex (p);
  fp = p - HexToPix (ip);
  sd = vec4 (4.);
  udm = vec4 (4.);
  for (int k = 0; k < 7; k ++) {
    u = Hashv2v2 (ip + gVec[k]);
    a = 2. * pi * (u.y - 0.5);
    d = hVec[k] + amp * (0.4 + 0.6 * u.x) * vec2 (cos (a), sin (a)) - fp;
    sd.w = dot (d, d);
    if (sd.w < sd.x) {
      sd = sd.wxyw;
      udm = vec4 (d, u);
    } else sd = (sd.w < sd.y) ? sd.xwyw : ((sd.w < sd.z) ? sd.xyww : sd);
  }
  sd.xyz = sqrt (sd.xyz);
#if DLEVEL >= 2
  return vec4 (SmoothMin (sd.y, sd.z, EPS) - sd.x, udm.xy, Hashfv2 (udm.zw));
#else
  return vec4 (min (sd.y, sd.z) - sd.x, udm.xy, Hashfv2 (udm.zw));
#endif
}

float GrndHt (vec2 p)
{
  vec4 sv;
  float h1, h2, r, s;
#if DLEVEL >= 3
  r = 0.2 * length (Noisev3v2 (0.3 * p));
#else
  r = 0.;
#endif
  sv = HexVor (0.05 * p + r);
  s = (0.8 + 0.5 * sv.w);
  h1 = s * smoothstep (0.1, 0.4 + 0.1 * sv.w, sv.x);
  h2 = 2. * s * smoothstep (0.2, 0.4 + 0.1 * sv.w, max (0.,
     (0.45 - dot (sv.yz, sv.yz))));
  return 3. * SmoothMax (h1, h2, 0.4) - 0.5;
}

float GrndRay (vec3 ro, vec3 rd)
{
  vec3 p;
  float dHit, h, s, sLo, sHi;
  s = 0.;
  sLo = 0.;
  dHit = dstFar;
  for (int j = 0; j < 200; j ++) {
    p = ro + s * rd;
    h = p.y - GrndHt (p.xz);
    if (h < 0.) break;
    sLo = s;
    s += max (0.2, 0.4 * h);
    if (s > dstFar) break;
  }
  if (h < 0.) {
    sHi = s;
    for (int j = 0; j < 5; j ++) {
      s = 0.5 * (sLo + sHi);
      p = ro + s * rd;
      if (p.y > GrndHt (p.xz)) sLo = s;
      else sHi = s;
    }
    dHit = 0.5 * (sLo + sHi);
  }
  return dHit;
}

vec3 GrndNf (vec3 p)
{
  vec2 e = vec2 (0.01, 0.);
  float h;
  h = GrndHt (p.xz);
  return normalize (vec3 (h - GrndHt (p.xz + e.xy), e.x, h - GrndHt (p.xz + e.yx)));
}

float FBallHit (vec3 ro, vec3 rd, vec3 p, float s)
{
  vec3 v;
  float h, b, d;
  v = ro - p;
  b = dot (rd, v);
  d = b * b + s * s - dot (v, v);
  h = dstFar;
  if (d >= 0.) {
    h = - b - sqrt (d);
    qHit = ro + h * rd;
    qnHit = (qHit - p) / s;
  }
  return h;
}

float FBallLum (vec3 ro, vec3 rd, float dHit)
{
  vec3 p, q, dp;
  float g, s, fr, f;
  p = ro + dHit * rd - fBallPos;
  dp = (fBallRad / 30.) * rd;
  g = 0.;
  for (int i = 0; i < 30; i ++) {
    p += dp;
    q = 4. * p;   q.y -= 5. * tCur;
    f = Fbm3 (q);
    q = 7. * p;   q.y -= 9. * tCur;
    f += Fbm3 (q);
    s = length (p);
    fr = max (1. - 0.9 * s / fBallRad, 0.);
    g += max (0.15 * fr * (f - 0.55), 0.);
    if (s > fBallRad || g > 1.) break;
  }
  return g;
}

vec3 SkyBg (vec3 rd)
{
  return vec3 (0.2, 0.3, 0.55) + 0.1 * pow (1. - max (rd.y, 0.), 4.);
}

vec3 SkyCol (vec3 ro, vec3 rd)
{
  vec3 col;
  float sd, f;
  sd = max (dot (rd, sunDir), 0.);
  ro.x += 0.5 * tCur;
  f = 0.5 * Fbm2 (0.1 * (rd.xz * (50. - ro.y) / rd.y + ro.xz));
  col = SkyBg (rd) + 0.35 * pow (sd, 6.) + 0.65 * min (pow (sd, 256.), 0.3);
  return mix (col, vec3 (0.85), clamp (f * rd.y + 0.1, 0., 1.));
}

float GrndSShadow (vec3 ro, vec3 rd)
{
  vec3 p;
  float sh, d, h;
  sh = 1.;
  d = 1.;
  for (int j = 0; j < 16; j ++) {
    p = ro + rd * d;
    h = p.y - GrndHt (p.xz);
    sh = min (sh, smoothstep (0., 0.05 * d, h));
    d += 0.3;
    if (sh < 0.05) break;
  }
  return 0.5 + 0.5 * sh;
}

vec3 ShowScene (vec3 ro, vec3 rd)
{
  vec3 flmCol, col, vn;
  float dstGrnd, dstFbHit, fIntens, f, sh;
  HexVorInit ();
#if DLEVEL == 5
  dstFbHit = FBallHit (ro, rd, fBallPos, fBallRad);
  fIntens = (dstFbHit < dstFar) ? FBallLum (ro, rd, dstFbHit) : 0.;
#endif
  dstGrnd = GrndRay (ro, rd);
  if (dstGrnd < dstFar) {
    ro += dstGrnd * rd;
    vn = GrndNf (ro);
    sh = 1.;
#if DLEVEL >= 4
    vn = VaryNf (1.3 * ro, vn, 3.);
    sh = GrndSShadow (ro, sunDir);
#endif
    col = mix (vec3 (0.9), vec3 (1.), clamp (0.7 * Noisefv2 (ro.xz) - 0.3, 0., 1.));
    col = col * (0.1 + 0.1 * max (vn.y, 0.) +
       0.2 * max (dot (vn, - sunDir), 0.) +
       0.8 * sh * max (0., max (dot (vn, sunDir), 0.))) +
       0.2 * sh * pow (max (dot (normalize (sunDir - rd), vn), 0.), 64.);
    f = dstGrnd / dstFar;
    f *= f;
    col = mix (col, SkyBg (rd), max (f * f - 0.1, 0.));
  } else col = SkyCol (ro, rd);
#if DLEVEL == 5
  if (dstFbHit < dstFar) {
    ro += rd * dstFbHit;
    rd = reflect (rd, qnHit);
    col = 0.9 * col + 0.08 + 0.25 * max (dot (qnHit, sunDir), 0.) * (1. +
       4. * pow (max (0., dot (sunDir, rd)), 128.));
  }
  f = clamp (0.7 * fIntens, 0., 1.);
  f *= f;
  flmCol = (1. + 0.4 * Noiseff (10. * tCur)) *
     mix (vec3 (1., 0.4, 0.1), vec3 (1., 1., 0.5), f * f);
  col = mix (col, flmCol, min (1.2 * fIntens * fIntens, 1.));
  if (dstFbHit < dstFar) {
    dstGrnd = GrndRay (ro, rd);
    col = mix (col, ((dstGrnd < dstFar) ? vec3 (0.4, 0.4, 0.5) :
       SkyCol (ro, rd)), pow (1. - abs (dot (rd, qnHit)), 3.));
  }
#endif
  return pow (clamp (col, 0., 1.), vec3 (0.7));
}

vec3 TrackPath (float t)
{
  return vec3 (30. * sin (0.035 * t) * sin (0.012 * t) * cos (0.01 * t) +
     26. * sin (0.0032 * t), 1. + 3. * sin (0.021 * t) * sin (1. + 0.023 * t), t);
}

void VuPM (float t)
{
  vec3 fpF, fpB, vel, acc, va, ort, cr, sr;
  float dt;
  dt = 2.;
  flPos = TrackPath (t);
  fpF = TrackPath (t + dt);
  fpB = TrackPath (t - dt);
  vel = (fpF - fpB) / (2. * dt);
  vel.y = 0.;
  acc = (fpF - 2. * flPos + fpB) / (dt * dt);
  acc.y = 0.;
  va = cross (acc, vel) / length (vel);
  ort = vec3 (0.2, atan (vel.z, vel.x) - 0.5 * pi, 5. * length (va) * sign (va.y));
  cr = cos (ort);
  sr = sin (ort);
  flMat = mat3 (cr.z, - sr.z, 0., sr.z, cr.z, 0., 0., 0., 1.) *
     mat3 (1., 0., 0., 0., cr.x, - sr.x, 0., sr.x, cr.x) *
     mat3 (cr.y, 0., - sr.y, 0., 1., 0., sr.y, 0., cr.y);
}

void mainImage (out vec4 fragColor, in vec2 fragCoord)
{
  mat3 vuMat;
  vec4 mPtr;
  vec3 ro, rd;
  vec2 canvas, uv, ori, ca, sa;
  float az, el;
  canvas = iResolution.xy;
  uv = 2. * fragCoord.xy / canvas - 1.;
  uv.x *= canvas.x / canvas.y;
  tCur = iTime;
  mPtr = iMouse;
  mPtr.xy = mPtr.xy / canvas - 0.5;
  az = 0.;
  el = 0.;
  VuPM (10. * tCur);
  ro = flPos;
  rd = normalize (vec3 (uv, 2.5));
  if (mPtr.z > 0.) {
    az = -2. * pi * mPtr.x;
    el = -0.1 * pi + pi * mPtr.y;
    ori = vec2 (el, az);
    ca = cos (ori);
    sa = sin (ori);
    vuMat = mat3 (ca.y, 0., - sa.y, 0., 1., 0., sa.y, 0., ca.y) *
       mat3 (1., 0., 0., 0., ca.x, - sa.x, 0., sa.x, ca.x);
    rd = vuMat * rd;
  } else {
    rd = rd * flMat;
  }
  fBallRad = 3.;
  fBallPos = TrackPath (10. * (tCur + 5. + 4. * sin (0.5 * tCur)));
  fBallPos.y += 10.;
  ro.y += 12.;
  sunDir = normalize (vec3 (cos (0.01 * tCur), 0.7, - sin (0.01 * tCur)));
  dstFar = 200.;
  fragColor = vec4 (ShowScene (ro, rd), 1.);
}

float SmoothMin (float a, float b, float r)
{
  float h;
  h = clamp (0.5 + 0.5 * (b - a) / r, 0., 1.);
  return mix (b, a, h) - r * h * (1. - h);
}

float SmoothMax (float a, float b, float r)
{
  return - SmoothMin (- a, - b, r);
}

const vec4 cHashA4 = vec4 (0., 1., 57., 58.);
const vec3 cHashA3 = vec3 (1., 57., 113.);
const float cHashM = 43758.54;

float Hashfv2 (vec2 p)
{
  return fract (sin (dot (p, cHashA3.xy)) * cHashM);
}

vec2 Hashv2f (float p)
{
  return fract (sin (p + cHashA4.xy) * cHashM);
}

vec4 Hashv4f (float p)
{
  return fract (sin (p + cHashA4) * cHashM);
}

vec2 Hashv2v2 (vec2 p)
{
  const vec2 cHashVA2 = vec2 (37.1, 61.7);
  const vec2 e = vec2 (1., 0.);
  return fract (sin (vec2 (dot (p + e.yy, cHashVA2),
     dot (p + e.xy, cHashVA2))) * cHashM);
}

vec4 Hashv4v3 (vec3 p)
{
  const vec3 cHashVA3 = vec3 (37.1, 61.7, 12.4);
  const vec3 e = vec3 (1., 0., 0.);
  return fract (sin (vec4 (dot (p + e.yyy, cHashVA3), dot (p + e.xyy, cHashVA3),
     dot (p + e.yxy, cHashVA3), dot (p + e.xxy, cHashVA3))) * cHashM);
}

float Noiseff (float p)
{
  float i, f;
  i = floor (p);  f = fract (p);
  f = f * f * (3. - 2. * f);
  vec2 t = Hashv2f (i);
  return mix (t.x, t.y, f);
}

float Noisefv2 (vec2 p)
{
  vec2 i = floor (p);
  vec2 f = fract (p);
  f = f * f * (3. - 2. * f);
  vec4 t = Hashv4f (dot (i, cHashA3.xy));
  return mix (mix (t.x, t.y, f.x), mix (t.z, t.w, f.x), f.y);
}

float Noisefv3a (vec3 p)
{
  vec3 i, f;
  i = floor (p);  f = fract (p);
  f *= f * (3. - 2. * f);
  vec4 t1 = Hashv4v3 (i);
  vec4 t2 = Hashv4v3 (i + vec3 (0., 0., 1.));
  return mix (mix (mix (t1.x, t1.y, f.x), mix (t1.z, t1.w, f.x), f.y),
              mix (mix (t2.x, t2.y, f.x), mix (t2.z, t2.w, f.x), f.y), f.z);
}

vec3 Noisev3v2 (vec2 p)
{
  vec4 h;
  vec3 g;
  vec2 ip, fp, ffp;
  ip = floor (p);
  fp = fract (p);
  ffp = fp * fp * (3. - 2. * fp);
  h = Hashv4f (dot (ip, cHashA3.xy));
  g = vec3 (h.y - h.x, h.z - h.x, h.x - h.y - h.z + h.w);
  return vec3 (h.x + dot (g.xy, ffp) + g.z * ffp.x * ffp.y,
     30. * fp * fp * (fp * fp - 2. * fp + 1.) * (g.xy + g.z * ffp.yx));
}

float Fbm2 (vec2 p)
{
  float f, a;
  f = 0.;
  a = 1.;
  for (int i = 0; i < 5; i ++) {
    f += a * Noisefv2 (p);
    a *= 0.5;
    p *= 2.;
  }
  return f;
}

float Fbm3 (vec3 p)
{
  const mat3 mr = mat3 (0., 0.8, 0.6, -0.8, 0.36, -0.48, -0.6, -0.48, 0.64);
  float f, a, am, ap;
  f = 0.;  a = 0.5;
  am = 0.5;  ap = 4.;
  p *= 0.5;
  for (int i = 0; i < 6; i ++) {
    f += a * Noisefv3a (p);
    p *= mr * ap;  a *= am;
  }
  return f;
}

float Fbmn (vec3 p, vec3 n)
{
  vec3 s = vec3 (0.);
  float a = 1.;
  for (int i = 0; i < 5; i ++) {
    s += a * vec3 (Noisefv2 (p.yz), Noisefv2 (p.zx), Noisefv2 (p.xy));
    a *= 0.5;
    p *= 2.;
  }
  return dot (s, abs (n));
}

vec3 VaryNf (vec3 p, vec3 n, float f)
{
  vec3 g;
  float s;
  const vec3 e = vec3 (0.1, 0., 0.);
  s = Fbmn (p, n);
  g = vec3 (Fbmn (p + e.xyy, n) - s, Fbmn (p + e.yxy, n) - s,
     Fbmn (p + e.yyx, n) - s);
  return normalize (n + f * (g - n * dot (n, g)));
}
void main(void)
{
    mainImage(gl_FragColor, gl_FragCoord.xy);
}