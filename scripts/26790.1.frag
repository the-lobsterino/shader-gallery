// perspective 80s grid
#ifdef GL_ES
precision mediump float;
#endif

uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

float prob_sum(float a, float b) {
	return 1.0 - (1.0 - a) * (1.0 - b);
}

void main( void ) {

	vec2 pos = gl_FragCoord.xy / resolution.xy;
	pos = pos * 2.0 - 1.0;
	pos *= 200.0;
	float z = 1.0 / (((gl_FragCoord.xy / resolution.xy).y+1.0));
	z = 1.0 - z;
	z*=4.0;
	pos.x *= z;

	float cyan = 0.0;
	float magenta = 0.0;
	
	float m = distance( mouse * resolution, pos ) / resolution.y;
	
	float xspeed = 0.0;
	float yspeed = -300.0;
	float cell_size = 40.0;
	float big_glow_size = 3.0 * pow( 0.5 + m, 0.8 );
	float small_glow_size = 1.0 * pow( 0.8 + m, 0.5 );
	
	float d;
	
	// Right side
	d = mod( pos.x + xspeed*time, cell_size );
	if ( d < big_glow_size )
		cyan = prob_sum(cyan, 1.0 - d / big_glow_size);
	if ( d < small_glow_size )
		magenta = prob_sum(magenta, 1.0 - d / small_glow_size);
	// Left side
	d = cell_size - d;
	if ( d < big_glow_size )
		magenta = prob_sum(magenta, 1.0 - d / big_glow_size);
	if ( d < small_glow_size )
		cyan = prob_sum(cyan, 1.0 - d / small_glow_size);
	// Top side
	d = mod( pos.y - yspeed*time, cell_size );
	if ( d < big_glow_size )
		cyan = prob_sum(cyan, 1.0 - d / big_glow_size);
	if ( d < small_glow_size )
		magenta = prob_sum(magenta, 1.0 - d / small_glow_size);
	// Bottom side
	d = cell_size - d;
	if ( d < big_glow_size )
		magenta = prob_sum(magenta, 1.0 - d / big_glow_size);
	if ( d < small_glow_size )
		cyan = prob_sum(cyan, 1.0 - d / small_glow_size);
	
	vec3 col = vec3( 0.0 );
	col.r = magenta;
	col.g = cyan;
	col.b = prob_sum(magenta, cyan);
//	col = vec3(z);
	gl_FragColor = vec4( col, 1.0 );

}