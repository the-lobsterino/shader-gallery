/*
 * Original shader from: https://www.shadertoy.com/view/MsSBWV
 */

#ifdef GL_ES
precision mediump float;
#endif

// glslsandbox uniforms
uniform float time;
uniform vec2 resolution;

// shadertoy emulation
#define iTime time
#define iResolution resolution

// --------[ Original ShaderToy begins here ]---------- //
//noise function from
//https://gist.github.com/patriciogonzalezvivo/670c22f3966e662d2f83
float rand(vec2 n) { 
	return fract(sin(dot(n, vec2(12.9898, 4.1414))) * 43758.5453);
}

float noise(vec2 p){
	vec2 ip = floor(p);
	vec2 u = fract(p);
	u = u*u*(3.0-2.0*u);
	
	float res = mix(
		mix(rand(ip),rand(ip+vec2(1.0,0.0)),u.x),
		mix(rand(ip+vec2(0.0,1.0)),rand(ip+vec2(1.0,1.0)),u.x),u.y);
	return res*res;
}
float mod289(float x){return x - floor(x * (1.0 / 289.0)) * 289.0;}
vec4 mod289(vec4 x){return x - floor(x * (1.0 / 289.0)) * 289.0;}
vec4 perm(vec4 x){return mod289(((x * 34.0) + 1.0) * x);}

float noise(vec3 p){
    vec3 a = floor(p);
    vec3 d = p - a;
    d = d * d * (3.0 - 2.0 * d);

    vec4 b = a.xxyy + vec4(0.0, 1.0, 0.0, 1.0);
    vec4 k1 = perm(b.xyxy);
    vec4 k2 = perm(k1.xyxy + b.zzww);

    vec4 c = k2 + a.zzzz;
    vec4 k3 = perm(c);
    vec4 k4 = perm(c + 1.0);

    vec4 o1 = fract(k3 * (1.0 / 41.0));
    vec4 o2 = fract(k4 * (1.0 / 41.0));

    vec4 o3 = o2 * d.z + o1 * (1.0 - d.z);
    vec2 o4 = o3.yw * d.x + o3.xz * (1.0 - d.x);

    return o4.y * d.y + o4.x * (1.0 - d.y);
}


float fbm(vec3 p) {
    float r = 0.0;
    float amp = 1.0;
    float freq = 1.0;
    for(int i = 0; i < 8; i++) {
        r += amp * noise(freq*p);
        amp *= 0.5;
        freq *= 1.0/0.5;
    }
    return r;
}
float fbm(vec2 p) {
    float r = 0.0;
    float amp = 1.0;
    float freq = 1.0;
    for(int i = 0; i < 10; i++) {
        r += amp * noise(freq*p);
        amp *= 0.5;
        freq *= 1.0/0.5;
    }
    return r;
}


float sdPlane(vec3 p, vec4 n) {
    return dot(p, n.xyz) + n.w;
}


float DF(vec3 p) {
    float d = sdPlane(p, vec4(0, 1, 0, 1));
    return d - 0.5*pow(fbm(p.xz), 2.0);
}


vec3 calcNormal(vec3 p) {
    float eps = 0.001;
    return normalize(vec3(
        DF(p + vec3(eps, 0, 0)) - DF(p + vec3(-eps, 0, 0)),
        DF(p + vec3(0, eps, 0)) - DF(p + vec3(0, -eps, 0)),
        DF(p + vec3(0, 0, eps)) - DF(p + vec3(0, 0, -eps))
    ));
}


struct Ray {
    bool hit;
    vec3 hitPos;
    vec3 hitNormal;
    int steps;
    float t;
};
const int maxSteps = 250;
Ray trace(vec3 from, vec3 rayDir) {
    float rayAngle = max(dot(rayDir, vec3(0, 0, 1)), 0.0);
    
    bool hit = false;
    vec3 hitPos = vec3(0);
    vec3 hitNormal = vec3(0);
    int steps = 0;
    float t = 0.1;
    for(int i = 0; i < maxSteps; i++) {
        vec3 p = from + t*rayDir;
        float d = 0.0;
        if(t < 10.0) {
            d = DF(p);
        }
        else {
            break;
        }
        if(d < 0.001) {
            hit = true;
            hitPos = p;
            hitNormal = calcNormal(p);
            steps = i;
            break;
        }
        t += 0.3*d;
    }
    return Ray(hit, hitPos, hitNormal, steps, t);
}


const vec3 sunDir = normalize(vec3(0, 1, 0.5));
const vec3 sunColor = vec3(1, 0.9, 0.3);
const vec3 skyColor = vec3(0.6, 0.8, 0.9);
vec3 sky(vec3 rayDir) {
    vec3 sun = pow(max(dot(rayDir, sunDir), 0.0), 12.0) * vec3(1, 0.8, 0.3);
    float theta = atan(rayDir.y/length(vec2(rayDir.x, rayDir.z)));
    float skyfactor = pow(abs(sin(theta)), 0.5);
    vec3 sky = skyfactor*skyColor + (1.0 - skyfactor)*vec3(1, 1, 0.9);
    return sky + sun;
}



float softShadow(vec3 hitPos, vec3 lightPos, float k) {
	vec3 lightDir = normalize(lightPos - hitPos);
    float ss = 1.0;
    float t = 0.1;
    for(int i = 0; i < 10; i++) {
        vec3 p = hitPos + t*lightDir;
        float d = DF(p);
        if(d < 0.001) {
            return 0.0;
        }
        ss = min(ss, k * d/t);
        t += d;
    }
    return ss;
}
float hardShadow(vec3 hitPos, vec3 lightPos) {
    Ray tr = trace(hitPos, normalize(lightPos - hitPos));
    if(tr.hit) {
        return 0.0;
    }
    else {
        return 1.0;
    }
}


float detailedAO(vec3 hitPos, vec3 hitNormal, float k) {
    float ao = 0.0;
    for(int i = 1; i <= 5; i++) {
        float d1 = float(i)/float(5) * k;
        vec3 p = hitPos + d1*hitNormal;
        ao += 1.0/pow(float(i), 2.0) * (d1 - DF(p));
    }
    return 1.0 - clamp(ao, 0.0, 1.0);
}


vec3 mountain_material(vec3 hitPos, vec3 hitNormal) {
    float theta = pow(1.0 - max(dot(hitNormal, vec3(0, 1, 0)), 0.0), 3.0);
    float height = hitPos.y + 0.8;
    if(theta > 0.3) {
        return vec3(1, 0.8, 0.5);
    }
    return vec3(0.5 + height, 0.5, 0) * vec3(1, 1.0 - 0.7*theta, 1.0 - theta);
}


void mainImage( out vec4 fragColor, in vec2 fragCoord ) {
    vec2 uv = (2.0*fragCoord.xy - iResolution.xy)/iResolution.y;
    
    vec3 camPos = vec3(3.0*cos(0.5*iTime), 0, iTime);
    camPos += vec3(0, noise(camPos.xz) - 0.7, 0);
    vec3 camFront = normalize(vec3(0.2*cos(0.5*iTime), 0, 1));
    vec3 camRight = cross(camFront, vec3(0, 1, 0));
    vec3 camUp = cross(camRight, camFront);
    vec3 rayDir = normalize(camFront + uv.x*camRight + uv.y*camUp);
    
    vec3 color = vec3(0);
    Ray tr = trace(camPos, rayDir);
    if(tr.hit) {
        float sAO = 1.0 - float(tr.steps)/float(maxSteps);
        //float dAO = detailedAO(tr.hitPos, tr.hitNormal, 1.0);
        float ss = softShadow(tr.hitPos, tr.hitPos + sunDir, 1.0);
        //float hs = hardShadow(tr.hitPos, tr.hitPos + sunDir);
        float diffuse = max(dot(tr.hitNormal, sunDir), 0.0);
        float fog = pow(exp(-0.1*tr.t), 2.0);
        vec3 mat = mountain_material(tr.hitPos, tr.hitNormal);
        
        color = fog * (ss*diffuse*mat*sunColor + 0.3*sAO*mat*skyColor) + (1.0 - fog)*vec3(1);
        //color = sAO * vec3(1);
        
        if(tr.hitPos.y < -0.95) {
            vec3 normal = vec3(0, 1, 0);
            vec3 refl = reflect(tr.hitPos, normal);
            Ray tr2 = trace(tr.hitPos, refl);
            vec3 reflColor = vec3(0);
            if(tr2.hit) {
                float diffuse = max(dot(tr2.hitNormal, sunDir), 0.0);
                vec3 mat = mountain_material(tr2.hitPos, tr2.hitNormal);
                reflColor = diffuse * mat;
            }
            else {
                reflColor = sky(reflect(rayDir, normal));
            }
            
            float dist = pow(abs(tr.hitPos.y + 0.95), 2.0);
            vec3 refrColor = mix(vec3(1, 1, 0.8), vec3(1, 1, 1), 300.0*dist);
            float f0 = 0.02;
            float fresnel = f0 + (1.0 - f0)*pow(1.0 - dot(-rayDir, normal), 5.0);
            vec3 specular = pow(max(dot(-rayDir, reflect(-sunDir, normal)), 0.0), 8.0) * vec3(1);
            color = fog * (mix(refrColor, reflColor, fresnel)) + (1.0 - fog)*vec3(1);
        }
    }
    else {
        color = sky(rayDir);
    }
    
    fragColor = vec4(color, 1.0);
}
// --------[ Original ShaderToy ends here ]---------- //

void main(void)
{
    //mainImage(gl_FragColor, gl_FragCoord.xy);
}