//An attempt at hex life rule described here http://www.well.com/~dgb/hexrules.html
//Reset by moving your mouse to the left side of the screen,
//randomize by moving it to the right side
#ifdef GL_ES
precision mediump float;
#endif

uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;
uniform sampler2D backbuffer;


const float hexR = 6.;


#define PI 3.14159265359
#define TAU 6.28318530718
#define deg60 1.0471975512
#define deg30 0.52359877559

// from https://glsl.heroku.com/e#15131.0
vec2 nearestHex(float s, vec2 st){
    float h = sin(deg30)*s;
    float r = cos(deg30)*s;
    float b = s + 2.0*h;
    float a = 2.0*r;
    float m = h/r;

    vec2 sect = st/vec2(2.0*r, h+s);
    vec2 sectPxl = mod(st, vec2(2.0*r, h+s));
    
    float aSection = mod(floor(sect.y), 2.0);
    
    vec2 coord = floor(sect);
    if(aSection > 0.0){
        if(sectPxl.y < (h-sectPxl.x*m)){
            coord -= 1.0;
        }
        else if(sectPxl.y < (-h + sectPxl.x*m)){
            coord.y -= 1.0;
        }
    }
    else{
        if(sectPxl.x > r){
            if(sectPxl.y < (2.0*h - sectPxl.x * m)){
                coord.y -= 1.0;
            }
        }
        else{
            if(sectPxl.y < (sectPxl.x*m)){
                coord.y -= 1.0;
            }
            else{
                coord.x -= 1.0;
            }
        }
    }
    
    float xoff = mod(coord.y, 2.0)*r;
    return vec2(coord.x*2.0*r-xoff, coord.y*(h+s))+vec2(r*2.0, s);
}


float maxNorm(vec3 x){
	return max(x.x,max(x.y,x.z));	
}

vec2 neighbourHex(vec2 x, float d, int i){
	return x-2.*d*vec2(cos(float(i)*deg60),sin(float(i)*deg60));
}


float rand(vec2 co){
  return fract(cos(dot(co.xy ,vec2(0.94393,3.394394))) * 43758.5453);
}


void main( void ) {

	if(mouse.x<0.01){
		gl_FragColor =	vec4(vec3(0.0),1.0);
		return;
	}
	
	vec3 col = vec3(0.0);
        //vec2 hexCoord = ( gl_FragCoord.xy);
	
	vec2 hexCoord = nearestHex(hexR, gl_FragCoord.xy);
	
	if(mouse.x>0.99){
		gl_FragColor =	vec4(vec3(rand(hexCoord*floor(time))>0.5),1.0);
		return;
	}
	
	float p = 0.;
	
	
	
	for(int i = 0; i<6; i++){
		vec2 n1 = neighbourHex(hexCoord,hexR,i); //Primary neighbour
		vec3 nc1 = texture2D(backbuffer,n1/resolution.xy).xyz;
		
		if(maxNorm(nc1)>0.01)
			p+=1.0; //Primary neighbour weight
		
		vec2 n2 = neighbourHex(n1,hexR,i+1); //Secondary neighbour
		vec3 nc2 = texture2D(backbuffer,n2/resolution.xy).xyz;
		if(maxNorm(nc2)>0.01)
			p+=0.30; //secondary neighbour weight
		
		
		col += nc1+0.3*nc2;
		
	}
	col /= maxNorm(col);
	
	vec3 selfcol = texture2D(backbuffer,hexCoord/resolution.xy).xyz;
	//Propagation Rule
	if(maxNorm(selfcol)>0.01){
		if((2.00 <= p) && (p <3.3))
			col = selfcol;
		else
			col = vec3(0.0);
	}else{
		if((2.3 <= p) && (p <= 2.9))
			col = col;
		else
			col = vec3(0.0);
	}
	
    	vec2 position = hexCoord/resolution.xy;	
	if(distance(resolution.xy*mouse,hexCoord)<=25.){
		col = vec3(0.5+0.5*sin(0.01*hexCoord.x),0.5+0.5*sin(0.01*hexCoord.y),0.5);
	}

    gl_FragColor = vec4(col, 1.0);
}