precision highp float;
uniform vec2  resolution;     // resolution (width, height)
uniform vec2  mouse;          // mouse      (0.0 ~ 1.0)
uniform float time;           // time       (1second == 1.0)

const float PI = 3.1415926;
const float PI2 = PI * 2.0;
const float EPS = 1e-4;

float dSphere(vec3 p, float r) {
    return length(p) - r;
}

mat2 rotate(float a) {
	float c = cos(a), s = sin(a);
	return mat2(c, s, -s, c);
}

float dMenger(vec3 z0, vec3 offset, float scale) {
    vec4 z = vec4(z0, 1.0);
    for (int n = 0; n < 10; n++) {
        z = abs(z);

        if (z.x < z.y) z.xy = z.yx;
        if (z.x < z.z) z.xz = z.zx;
        if (z.y < z.z) z.yz = z.zy;

        z *= scale;
        z.xyz -= offset * (scale - 1.0);

        if (z.z < -0.5 * offset.z * (scale - 1.0)) {
            z.z += offset.z * (scale - 1.0);
        }
    }
    return length(max(abs(z.xyz) - vec3(1.0), 0.0)) / z.w;
}

vec2 foldRotate(vec2 p, float s) {
    float a = PI / s - atan(p.x, p.y);
    float n = PI2 / s;
    a = floor(a / n) * n;
    p = rotate(a) * p;
    return p;
}

vec3 opRep(vec3 p, vec3 c) {
	return mod(p, c) - 0.5 * c;
}

float dScene(vec3 p) {
	p -= vec3(2.0);
	p = opRep(p, vec3(4.0, 4.0, 2.0));	
	p.xy = foldRotate(p.xy, 8.0);
	float d = dMenger(p, vec3(0.8, 1.1 + 0.3 * sin(time), 0.5), 2.3);
	return d;
}

void main() {
	vec2 uv = (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y);

	float t = time;

	vec3 ro = vec3(0.0, 0.0, -2.0 + t);
	vec3 ta = vec3(0.0, 0.0, 0.0 + t);

	vec3 fwd = normalize(ta - ro);
	vec3 right = cross(fwd, vec3(0, 1, 0));
	vec3 up = cross(right, fwd);
	vec3 rd = normalize(fwd + uv.x * right + uv.y * up);

	float distance = 0.0;
	vec3 p = ro;
	int step = 0;
	for (int i = 0; i < 80; i++) {
		step = i;
		float d = dScene(p);
		distance += d;
		p = ro + distance * rd;
		if (abs(d) < EPS) break;
	}

	vec3 c = vec3(float(step) * 0.01);
	gl_FragColor = vec4(gl_FragCoord.xy / resolution.xy, 0.0, 1.0);
	gl_FragColor = vec4(c, 1.0);
}